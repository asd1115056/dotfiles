### Added by Zinit's installer
if [[ ! -f $HOME/.local/share/zinit/zinit.git/zinit.zsh ]]; then
    print -P "%F{33} %F{220}Installing %F{33}ZDHARMA-CONTINUUM%F{220} Initiative Plugin Manager (%F{33}zdharma-continuum/zinit%F{220})â€¦%f"
    command mkdir -p "$HOME/.local/share/zinit" && command chmod g-rwX "$HOME/.local/share/zinit"
    command git clone https://github.com/zdharma-continuum/zinit "$HOME/.local/share/zinit/zinit.git" && \
        print -P "%F{33} %F{34}Installation successful.%f%b" || \
        print -P "%F{160} The clone has failed.%f%b"
fi

source "$HOME/.local/share/zinit/zinit.git/zinit.zsh"
autoload -Uz _zinit
(( ${+_comps} )) && _comps[zinit]=_zinit

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zinit light-mode for \
    zdharma-continuum/zinit-annex-as-monitor \
    zdharma-continuum/zinit-annex-bin-gem-node \
    zdharma-continuum/zinit-annex-patch-dl \
    zdharma-continuum/zinit-annex-rust

### End of Zinit's installer chunk

# Load starship theme
# line 1: `starship` binary as command, from github release
# line 2: starship setup at clone(create init.zsh, completion)
# line 3: pull behavior same as clone, source init.zsh
zinit ice as"command" from"gh-r" \
          atclone"./starship init zsh > init.zsh; ./starship completions zsh > _starship" \
          atpull"%atclone" src"init.zsh"
zinit light starship/starship

#######################################################
# ZSH Basic Options
#######################################################

setopt autocd              # change directory just by typing its name
# setopt correct             # auto correct mistakes
setopt interactivecomments # allow comments in interactive mode
setopt magicequalsubst     # enable filename expansion for arguments of the form 'anything=expression'
setopt nonomatch           # hide error message if there is no match for the pattern
setopt notify              # report the status of background jobs immediately
setopt numericglobsort     # sort filenames numerically when it makes sense
setopt promptsubst         # enable command substitution in prompt

# History options
setopt hist_ignore_space   # ignore commands starting with space (for Atuin dedup)
setopt hist_reduce_blanks  # remove superfluous blanks from history items
setopt hist_ignore_all_dups # remove older duplicate entries from history
setopt hist_save_no_dups   # don't save duplicate entries

#######################################################
# Environment Variables
#######################################################

# FZF - Use fd for file/directory search (faster, respects .gitignore)
if command -v fd &>/dev/null; then
    export FZF_DEFAULT_COMMAND='fd --type f --hidden --follow --exclude .git'
    export FZF_CTRL_T_COMMAND="$FZF_DEFAULT_COMMAND"
    export FZF_ALT_C_COMMAND='fd --type d --hidden --follow --exclude .git'
fi

# FZF - Default options
export FZF_DEFAULT_OPTS="
  --height 60%
  --border rounded
  --layout reverse
  --info inline
  --preview-window=right:60%:wrap
  --bind 'ctrl-/:toggle-preview'"

# FZF - Ctrl+T file search with preview
export FZF_CTRL_T_OPTS="
  --preview 'bat --color=always --style=numbers --line-range=:500 {} 2>/dev/null || cat {}'"

# FZF - Alt+C directory search with preview
export FZF_ALT_C_OPTS="
  --preview 'eza --tree --level=2 --color=always {} 2>/dev/null || ls -la {}'"

#######################################################
# Plugin Configuration
#######################################################

#Plugin - zsh-autocomplete, autosuggestions, syntax-highlighting
zinit ice lucid atinit"ZINIT[COMPINIT_OPTS]=-C; zicompinit; zicdreplay"
zinit light marlonrichert/zsh-autocomplete

zinit lucid for \
  zsh-users/zsh-autosuggestions

#Plugin - reminds you to use existing aliases
zinit light MichaelAquilina/zsh-you-should-use

#Plugin - command-line fuzzy finder
zinit ice as"command" from"gh-r" \
    mv"fzf*/fzf -> fzf" \
    atclone"./fzf --zsh > init.zsh" \
    atpull"%atclone" src"init.zsh"
zinit light junegunn/fzf

#Plugin - interactive git commands with fzf
zinit ice lucid wait'1'
zinit light wfxr/forgit

#Plugin - modern replacement for ls with better colors and features
zinit ice as"command" from"gh-r" \
    mv"eza*/eza -> eza" \
    atload"alias ls='eza --icons=auto --group-directories-first'; \
           alias ll='eza -lh --icons=auto --group-directories-first'; \
           alias la='eza -lha --icons=auto --group-directories-first'; \
           alias lt='eza --tree --level=2 --icons=auto'; \
           alias lg='eza -lh --icons=auto --group-directories-first --git'"
zinit light eza-community/eza

#Plugin - automatically ls after cd
zinit light zshzoo/cd-ls

#Plugin - cat clone with syntax highlighting and git integration
zinit ice as"command" from"gh-r" \
    mv"bat*/bat -> bat" \
    atload"alias cat='bat --paging=never --style=plain'; \
           alias batman='bat --paging=always --style=full'; \
           export MANPAGER='sh -c \"col -bx | bat -l man -p\"'"
zinit light sharkdp/bat

#Plugin - simplified and community-driven man pages
zinit ice as"command" from"gh-r" bpick"*linux*.tar.gz" \
    mv"tlrc*/tlrc -> tldr" \
    atload"alias help='tldr'"
zinit light tldr-pages/tlrc

#Plugin - smarter cd command (intelligent directory navigation)
# Note: using 'zz' for interactive mode to avoid conflict with zinit's 'zi' alias
zinit ice as"command" from"gh-r" \
    mv"zoxide*/zoxide -> zoxide" \
    atclone"./zoxide init zsh --cmd z > init.zsh" \
    atpull"%atclone" src"init.zsh" \
    atload"alias zz='__zoxide_zi'"
zinit light ajeetdsouza/zoxide

#Plugin - press ESC twice to add sudo to current command
zinit snippet OMZ::plugins/sudo/sudo.plugin.zsh

#Plugin - copy utilities (file contents and paths to clipboard)
zinit snippet OMZ::plugins/copyfile/copyfile.plugin.zsh
zinit snippet OMZ::plugins/copypath/copypath.plugin.zsh

# Syntax highlighting must be loaded last
zinit ice lucid wait'0'
zinit light zdharma-continuum/fast-syntax-highlighting

#Plugin - magical shell history with search and sync
zinit ice as"command" from"gh-r" bpick"atuin-*.tar.gz" mv"atuin*/atuin -> atuin" \
    atclone"./atuin init zsh > init.zsh; ./atuin gen-completions --shell zsh > _atuin" \
    atpull"%atclone" src"init.zsh"
zinit light atuinsh/atuin
